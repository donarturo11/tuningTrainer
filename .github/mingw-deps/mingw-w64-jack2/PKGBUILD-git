# Maintainer: Fabrizio del Tin <fdt@euniversity.pub>
# Contributor: Tim Mayberry <mojofunk@gmail.com>

_realname=jack2
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.9.14
pkgrel=1
pkgdesc="The Jack Audio Connectin Kit (mingw-w64)"
arch=('any')
url="http://jackaudio.org"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             'python3')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libsamplerate"
         "${MINGW_PACKAGE_PREFIX}-portaudio"
         "${MINGW_PACKAGE_PREFIX}-libsndfile"
         "${MINGW_PACKAGE_PREFIX}-readline")

options=('staticlibs') # '!strip' 'debug')
source=("https://github.com/jackaudio/jack2/releases/download/v${pkgver}/v${pkgver}.tar.gz")

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"
  sed -i '1 i\#ifndef ADDON_DIR\n    #define ADDON_DIR "jack"\n#endif\n' common/JackTools.h
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export PKG_CONFIG_PATH=${MINGW_PREFIX}/lib/pkgconfig
  export PKGCONFIG=/usr/bin/pkg-config.exe
  /usr/bin/python3 ./waf configure --prefix=${MINGW_PREFIX}
  /usr/bin/python3 ./waf build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python3 ./waf --destdir="${pkgdir}" install
  # jack2 creates an import lib with same name as portaudio
  # this should be fixed at some point in jack2 build
  rm "${pkgdir}${MINGW_PREFIX}/bin/jack/"*portaudio*dll.a
  cp ${srcdir}/${_realname}-${pkgver}/windows/portaudio/pa_asio.h ${pkgdir}${MINGW_PREFIX}/include
}
md5sums=('b63b5cf65fc43ccf9585d274fe91069f')
